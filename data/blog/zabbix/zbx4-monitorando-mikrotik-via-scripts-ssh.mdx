---
title: 'Monitoramento de PPPoE, DHCP e OSPF por Interface no Mikrotik usando scripts' 
date: '2025-03-10'
tags: ['zabbix']
draft: true
summary: Este artigo fornece orientações para criação de scripts que podem ser integrados ao Zabbix para monitoramento de link de internet.
---

## Introdução
Embora o Mikrotik ofereça suporte a SNMP para monitoramento, nem todos os parâmetros ou estados são facilmente exportáveis por ele, especialmente em cenários mais complexos. Muitas informações dinâmicas — como leases de DHCP por interface, sessões PPPoE e adjacências OSPF — não estão disponíveis para monitoramento via SNMP.

O Mikrotik RouterOS possui uma linguagem de script própria, que apesar de limitada em alguns aspectos, permite realizar tarefas de consulta, processamento e formatação de dados.

Ao estruturarmos a saída dos scripts em formato JSON, facilitamos a integração com ferramentas como o Zabbix, (ou qualquer sistema que consiga fazer parsing de JSON). Essa abordagem reduz o esforço de tratamento da resposta, tornando o monitoramento muito mais direto e padronizado.

### Link para o tutorial em vídeo
<YouTube id="L3BJZMVtCCQ"/>

---

## Explicação geral do funcionamento dos scripts
Os scripts tem como objetivo listar, em formato JSON, informações da Mikrotik que não podem ser acessadas via monitoramento SNMP. 
- Interface
- Nome do servidor DHCP
- Quantidade de leases

Você deverá salvar o script abaixo em *System -> Scripts*. Nomeie como `monitoramento_dhcp` e a única permissão necessária é a de *read* (leitura).

## Quantidade de Leases DHCP por Interface
```python
:global lista [/ip dhcp-server find];
:global total [:len $lista];
:global contador 0;

:put "{ \"data\": [";

:foreach i in=$lista do={
    :local servidor [/ip dhcp-server get value-name=name $i];
    :local interface [/ip dhcp-server get value-name=interface $i];
    :local leases [:len [/ip dhcp-server lease find where server=$servidor]];

    :if ($contador < $total) do={
        :if ($contador = ($total - 1)) do={
            :put ("{ \"interface\": \"$interface\", \"servidor\": \"$servidor\", \"dados\": { \"leases\": $leases } }");
        } else={
            :put ("{ \"interface\": \"$interface\", \"servidor\": \"$servidor\", \"dados\": { \"leases\": $leases } },");
        }
    };
    :set contador ($contador + 1);
};

:put "] }\n";
```

### Configuração do item no Zabbix

Vamos usar dois itens, um item normal, que irá executar o script e receber o retorno de todas as entradas e um item de descoberta automática dependente, que para cada entrada irá criar um item de monitoramento.

Para o item mestre, configure como a imagem abaixo:



```js
$.data[?(@.interface == '{#INTERFACE}')].dados.leases.first()
```
---

## Quantidade de sessões PPPoE por Interface
```python
:global lista [/interface pppoe-server server find];
:global total [:len $lista];
:global contador 0;

:put "{ \"data\": [";

:foreach i in=$lista do={
    :local servidor [/interface pppoe-server server get value-name=service-name $i];
    :local clientesAtivos [:len [/interface pppoe-server find where service=$servidor]];

    :if ($contador < $total) do={
        :if ($contador = ($total - 1)) do={
            :put ("{ \"servidor\": \"$servidor\", \"dados\": { \"clientesAtivos\": $clientesAtivos } }");
        } else={
            :put ("{ \"servidor\": \"$servidor\", \"dados\": { \"clientesAtivos\": $clientesAtivos } },");
        }
    };
    :set contador ($contador + 1);
};

:put "] }\n";
```

```js
$.data[?(@.servidor== '{#SERVIDOR}')].dados.clientesAtivos.first()
```

---

## Estado do vizinho OSPF
```python
:global lista [/routing ospf neighbor find];
:global total [:len $lista];
:global contador 0;

:put "{ \"data\": [";

:foreach i in=$lista do={
    :local routerId [/routing ospf neighbor get value-name=router-id $i];
    :local address [/routing ospf neighbor get value-name=address $i];
    :local status [/routing ospf neighbor get value-name=state $i];
    :local uptime [/routing ospf neighbor get value-name=adjacency $i];
    :local quantidadeMudancas [/routing ospf neighbor get value-name=state-changes $i];

    :if ($contador < $total) do={
        :if ($contador = ($total - 1)) do={
            :put ("{ \"router_id\": \"$routerId\", \"address\": \"$address\", \"dados\": { \"status\": \"$status\", \"uptime\": \"$uptime\", \"mudancas_estado\": $quantidadeMudancas } }");
        } else={
            :put ("{ \"router_id\": \"$routerId\", \"address\": \"$address\", \"dados\": { \"status\": \"$status\", \"uptime\": \"$uptime\", \"mudancas_estado\": $quantidadeMudancas } },");
        }
    };
    :set contador ($contador + 1);
};

:put "] }\n";

```

```js
$.data[?(@.router_id == '{#ROUTER_ID}' && @.address == '{#ADDRESS}')].dados.status.first()
```
